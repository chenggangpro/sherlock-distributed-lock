apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'de.marcphilipp.nexus-publish'

nexusStaging {
  packageGroup = "com.coditory"
  stagingProfileId = "365d557aaf167c"
}

if (System.env.NEXUS_USERNAME) {
  project.allprojects {
    project.nexusUsername = System.env.NEXUS_USERNAME
    project.nexusPassword = System.env.NEXUS_PASSWORD
  }
}

// Update readme on release only
if (!project.version.endsWith("-SNAPSHOT")) {
  scmVersion {
    hooks {
      // update version in readme.md
      pre 'fileUpdate', [
          file       : 'readme.md',
          pattern    : { v, p -> v },
          replacement: { v, p -> v }
      ]
      pre 'commit'
    }
  }
}

// Don't publish the default jar for the root project
// and other not publishable modules
configurations.archives.artifacts.clear()
subprojects
    .findAll { notPublishableModules.contains(it.path) }
    .each { subproject ->
      configurations.archives.artifacts.clear()
    }

if (project.hasProperty('publish')) {
  subprojects
      .findAll { !notPublishableModules.contains(it.path) }
      .each { subproject ->
        subproject.apply from: "${rootDir}/gradle/publish-project.gradle"
      }
}

task publishRelease() {
  dependsOn 'publish'
  dependsOn 'closeAndReleaseRepository'
  tasks.findByName('closeAndReleaseRepository').mustRunAfter 'publish'
}
