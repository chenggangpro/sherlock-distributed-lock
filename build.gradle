buildscript {
    ext {
        versions = [
                reactor        : '3.4.13',
                rxjava         : '2.2.21',
                mongodb        : '4.4.0',
                mongodbReactive: '4.4.0',
                slf4j          : '1.7.32',
                logback        : '1.2.10',
                postgresql     : '42.3.1',
                mysql          : '8.0.27',
                // test dependencies:
                jsonAssert     : '1.5.0',
                testContainers : '1.16.2',
                spock          : '2.1-M2-groovy-3.0'
        ]
        notPublishableModules = [
                ':api',
                ':sample',
                ':mongo',
                ':tests'
        ]
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'jacoco'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'com.coditory.build' version '0.1.15' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

repositories {
    mavenCentral()
}

description = 'Distributed Lock Library for JVM'

allprojects {
    group = 'com.coditory.sherlock'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'com.coditory.build'
    sourceCompatibility = 11

    // make nicer archive and module names
    archivesBaseName = rootProject.name + '-' + project.name
    ext.moduleName = archivesBaseName.replaceAll('-', '.')

    dependencies {
        testImplementation "ch.qos.logback:logback-core:$versions.logback"
        testImplementation "ch.qos.logback:logback-classic:$versions.logback"
        testImplementation "org.spockframework:spock-core:$versions.spock"
    }
}

apply from: "gradle/coverage.gradle"

if (project.hasProperty("release") && project.property("release") == "true") {
    apply from: "gradle/release.gradle"
}
